plugins {
    id 'java'
    id 'pmd'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.24.0'
}

group = 'com.mimaja'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot application for mobile JobFinderApp'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = '0.8.10'
}

spotless {
    java {
        target("src/*/java/**/*.java")
        cleanthat().excludeMutator("ModifierOrder")
        importOrder()
        removeUnusedImports()

        googleJavaFormat("1.23.0").aosp().reflowLongStrings()

        trimTrailingWhitespace()
        endWithNewline()
        indentWithSpaces()
        formatAnnotations()
        toggleOffOn()
        lineEndings = com.diffplug.spotless.LineEnding.UNIX

        replaceRegex("One blank line after package line", "(package .+;)\n+import", '$1\n\nimport')
        replaceRegex("One blank line after import lists", "(import .+;\n\n)\n+", '$1')
        replaceRegex("Remove wildcard imports", "import( static)?\\s+[^*\\s]+\\*;(\\r\\n|\\r|\\n)", '\$2')
        replaceRegex("Remove empty lines after start of block", "(\\{)\\s*\\n\\s*\\n", '$1\n')
        replaceRegex("Remove empty lines before end of block", "\\n[\\n]+(\\s*})(?=\\n)", '\n$1')
        replaceRegex("Remove trailing empty comment lines.", "\n\\s*\\*(\n\\s*\\*/\n)", '$1')
    }
    format("styling") {
        target(
                '**/*.md',
                '**/*.yml',
                '**/*.yaml',
                '**/*.json'
        )
        prettier()
    }
}

pmd {
    toolVersion = '6.52.0'
    ignoreFailures = false
    ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml"]
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
  implementation 'com.auth0:java-jwt:4.0.0'
  implementation 'software.amazon.awssdk:s3:2.31.50'
  implementation 'software.amazon.awssdk:apache-client:2.31.50'
  runtimeOnly 'org.postgresql:postgresql'

  // Lombok
  compileOnly "org.projectlombok:lombok:1.18.30"
  annotationProcessor "org.projectlombok:lombok:1.18.30"

  // MapStruct - runtime API + processor
  implementation "org.mapstruct:mapstruct:1.6.3"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.6.3"

  // Ensure MapStruct sees Lombok-generated methods during APT
  annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.mockito:mockito-core:5.2.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  implementation 'com.google.api-client:google-api-client:1.34.1'
}

sourceSets {
  main {
    java {
      srcDir "$buildDir/generated/sources/annotationProcessor/java/main"
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.compilerArgs += [
    '-Amapstruct.defaultComponentModel=spring',
    '-Amapstruct.verbose=true'
  ]
}

tasks.named('test') {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required.set(true)
    html.required.set(true)
    csv.required.set(false)
  }
}

tasks.named('check') {
  dependsOn jacocoTestReport
}
